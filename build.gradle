import org.ajoberstar.grgit.*
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath group: 'kr.motd.gradle', name: 'sphinx-gradle-plugin', version: '1.0.6'
    classpath group: 'org.ajoberstar', name: 'grgit', version: '2.1.0'
  }
}

defaultTasks 'docs'

// Get version information from GIT branch.
def loadGitBranchInformation() {
	def repo = Grgit.open {
		dir = project.file('.')
	}
	println 'Working from branch ' + repo.branch.current.name
	if (repo.branch.current.name.length() > 10) {
		project.ext.gitBranchName = repo.branch.current.name.substring(10)
	} else {
		println 'Travis using branch ' + System.getenv('REAL_BRANCH')
		project.ext.gitBranchName = System.getenv('REAL_BRANCH').substring(10)
	}
	repo.close()
}

apply plugin: 'kr.motd.sphinx'
sphinx {
	// Load information for current branch.
	loadGitBranchInformation()
	
	outputDirectory = "${buildDir}/sitewhere.github.io/docs/en/" + project.ext.gitBranchName
}

apply plugin: 'java'

// Prepare by pulling sitewhere gh-pages repository.
task beforeSphinxBuild() {
	// Load username/password from Gradle properties or ENV variables.
	def username = project.hasProperty('docgenUser') ? docgenUser : System.getenv('DOCGEN_USER')
	def password = project.hasProperty('docgenPassword') ? docgenPassword : System.getenv('DOCGEN_PASSWORD')
	
	// Clone SiteWhere repository.
	delete "${buildDir}/sitewhere.github.io"
	project.ext.gitRepository = Grgit.clone(dir: "${buildDir}/sitewhere.github.io", 
		uri: 'https://github.com/sitewhere/sitewhere.github.io.git', refToCheckout: 'master',
		credentials: new Credentials(username, password))
	
	// Remove existing documentation for generated version.
	delete "${buildDir}/sitewhere.github.io/docs/en/" + project.ext.gitBranchName
}

task buildSphinxConfig(type: Copy) {
    from 'src/templates/sphinx'
    into 'src/site/sphinx'
    include 'conf.py'
    filter(ReplaceTokens, tokens: [version: project.ext.gitBranchName])
}

task docs(dependsOn: ['beforeSphinxBuild', 'buildSphinxConfig', 'site']) {
	doLast {
		delete "${buildDir}/sitewhere.github.io/docs/en/" + project.ext.gitBranchName + "/.doctrees"
		
		project.ext.gitRepository.add(patterns: ["docs/en/" + project.ext.gitBranchName], update: false) 
		project.ext.gitRepository.add(patterns: ["docs/en/" + project.ext.gitBranchName], update: true)
		project.ext.gitRepository.commit(message: 'Automated documentation update.')
		project.ext.gitRepository.push(refsOrSpecs: ['master'])
		
		// Close repository.
		project.ext.gitRepository.close()
	}
}